####流程控制
流程控制语句用来控制程序执行的流程, 实现程序逻辑. 常见的流程控制语句有:

* If
* If Else
* If ElseIf Else
* Select Case
* Do...Loop
* For...Next

下面逐个介绍

#####If语句
If 语句的格式如下:

```
If (逻辑表达式) Then
	若干操作
End If
```

`逻辑表达式` 就是表达式结果为布尔型( True 或 False)的表达式, 举个例子

```
Option Explicit
Sub demo0501()
    Dim age As Integer
    age = 5
    If (age < 10) Then
        MsgBox ("Child")
    End If
End Sub
```

运行子程序 `demo0501` 会弹出一个消息框, 修改 `age = 5` 为 `age  = 20` 再运行, 则不会弹出消息框.

可以看出, If 语句的功能为: 如果逻辑表达式结果为 True, 就执行 If 语句包围的代码段, 如果表达式为 False, 就跳过该代码段.

#####If Else语句
类似的, If Else语句的格式如下:

```
If (逻辑表达式) Then
    若干操作
Else
    其他操作
End If
```
代码段的执行逻辑为: 如果 `逻辑表达式` 为 True, 则执行 `若干操作`, 跳过 `其他操作`; 如果 `逻辑表达式` 为 False, 则执行 `其他操作`, 跳过 `若干操作`.

示例代码:

```
Option Explicit
Sub demo0502()
    Dim age As Integer
    age = 15
    If (age < 10) Then
        MsgBox ("Child")
    Else
        MsgBox ("Not a child")
    End If
End Sub
```

#####If ElseIf Else语句
If ElseIf Else语句的格式如下:

```
If (逻辑表达式1) Then
   操作1
ElseIf (逻辑表达式2) Then
   操作2
ElseIf (逻辑表达式n) Then
   操作n
Else
   操作n+1
End If
```

其中 ElseIf 语句段可以有 0-n 个. 当然, 有 0 个时, 就变成了 If Else 语句.


#####Select Case语句
Select Case语句的格式如下:

```
Select Case (表达式)
   Case 值1
      操作1
   Case 值2
      操作2
   Case 值n
      操作n   
  Case Else
      操作n+1
End Select
```

该语句计算 `表达式` 的值, 在 `值1`/`值2`/`值n` 中找到匹配的值, 如果找到匹配的值, 则执行对应操作, 否则, 执行 `Case Else` 段中的 `操作n+1`

示例代码:

```
Option Explicit
Sub demo0503()
    Dim age As Integer
    age = 15
    Select Case age
        Case 10
            MsgBox ("Same age with my sister")
        Case 15
            MsgBox ("Same age with me")
        Case Else
            MsgBox ("No same age")
    End Select
End Sub
```

这个语句中的 `Case 值1` 也可以以 `Case Is 比较表达式` 或者 `Case 值1 To 值n` 的方式使用, 较为复杂, 这里不再详细介绍, 具体可参考[Select Case 语句](https://msdn.microsoft.com/ZH-CN/library/office/gg278665.aspx)

#####Do...Loop语句
Do...Loop语句的格式如下:

```
Do While 逻辑表达式
	若干操作
Loop
```

语句判断 `逻辑表达式`, 只要为 True, 就一直执行 `若干操作`, 直到 `逻辑表达式` 为 False.

举个例子, 下面代码计算 `1+2+...+10` 的结果:

```
Option Explicit
Sub demo0504()
    Dim sum As Long
    Dim i As Integer
    
    sum = 0
    i = 1
    Do While i <= 100
        sum = sum + i
        i = i + 1
    Loop
    MsgBox (sum)
End Sub
```

运行子程序会弹出对话框, 显示 `5050`.

Do...Loop语句也可以是以下形式:

```
Do
	若干操作
	If (逻辑表达式) Then
		Exit Do
	End If
Loop
```

按照这个形式, 前面的 `demo0504` 的例子可以改写为:

```
Option Explicit
Sub demo0505()
    Dim sum As Long
    Dim i As Integer
    
    sum = 0
    i = 1
    Do
        sum = sum + i
        i = i + 1
        If (i > 100) Then
            Exit Do
        End If
    Loop
    MsgBox (sum)
End Sub
```

更多 Do...Loop 形式参考 [Do...Loop 语句](https://msdn.microsoft.com/ZH-CN/library/office/gg278815.aspx)

#####For...Next语句
For...Next语句的形式为:

```
For 计数器 = 起始值 To 结束值 [Step 值]
    若干操作
Next
```

为了直观, 使用例子来感受一下:

```
Option Explicit
Sub demo0506()
    Dim i As Integer
    For i = 1 To 5
        MsgBox (i)
    Next
End Sub
```

运行子程序, 会弹出从 5 次对话框, 分别显示 1-5.  
前面表达式中的 `[Step 值]` 为可选, 表示 `计数器` 从 `起始值` 到 `结束值` 累加的过程中, 每次加的步长.

修改前面的例子:

```
Option Explicit
Sub demo0507()
    Dim i As Integer
    For i = 1 To 5 Step 3
        MsgBox (i)
    Next
End Sub
```

执行结果为弹出两次值为 1 和 4 的对话框.  

step 也可以为负数, 来实现逆向的循环, 例如我们将第一个例子变成逆向, 如下: 

```
Option Explicit
Sub demo0508()
    Dim i As Integer
    For i = 5 To 1 Step -1
        MsgBox (i)
    Next
End Sub
```

在 `若干操作` 中, 随时都可以使用 `Exit For` 来跳出这个 For...Next 循环. 就像 Do...Loop 中的 `Exit Do` 一样.


#####其他说明
本节中的 `(逻辑表达式)` 中的括号可以省略

[上一节](04.md) [下一节](06.md)

